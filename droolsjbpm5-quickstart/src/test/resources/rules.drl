package com.plugtree.examples;

import com.plugtree.examples.model.Person;
import com.plugtree.examples.model.Pet;
import com.plugtree.examples.model.Firefighter;
import com.plugtree.examples.model.Pet.PetType;
import java.util.HashMap;
import java.util.Map;

global org.drools.runtime.process.ProcessRuntime processRuntime;

// The rules for the cat in the tree example

rule "Call the fire department"
    when
        $p: Person($pet: pet, petCallCount > 0)
        $cat: Pet(this == $pet, position == "on a limb", type == PetType.CAT)
    then
       System.out.println($p.getName()+": Hello... Fire Department? My Cat is on a tree. Please help me.");
       Firefighter firefighter = new Firefighter("Fred");
       insert(firefighter);
end

rule "Firefighter gets the cat down"
    when
        $f: Firefighter()
        $p: Person($pet: pet, petCallCount > 0)
        $cat: Pet(this == $pet, position == "on a limb", type == PetType.CAT)
    then
       System.out.println("Firefighter " +$f.getName()+": Start process: getting the cat down.");
       Map params = new HashMap();
       params.put("fireFighter", $f);
       params.put("cat", $cat);
       processRuntime.startProcess("com.plugtree.examples.process", params);
end

rule "Call a Cat when it is in a tree"
    when
        $p: Person($pet: pet, petCallCount == 0) 
        $cat: Pet(this == $pet, position == "on a limb", type == PetType.CAT)
    then
       System.out.println($p.getName()+": "+$cat.getName() + " come down!");
       $p.setPetCallCount($p.getPetCallCount()+1);
       update($p);
end

